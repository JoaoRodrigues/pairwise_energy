!
! Calculates pairwise atom-atom energies at the interface.
! Does *NOT* rebuild or minimize the structure. Use only for HADDOCK models.
! Adapted from HADDOCK protocols here and there.
! JR @ 2015
!

evaluate ($coorfile = "example/e2a_hpr.pdb")
evaluate ($interface_dist_cutoff = 5.0)

! Mute (most) output
set message=off echo=off end

!
! Read parameter and topology databases
!

parameter
    @@toppar/protein-allhdg5-4.param
    @@toppar/ion.param
    @@toppar/ligand.param
end

parameter
  nbonds
    nbxmod=5 atom cdie shift
    cutnb=9.5 ctofnb=8.5 ctonnb=6.5 eps=1.0 e14fac=0.4 inhibit 0.25
    wmin=0.5 tolerance  0.5
  end
end

topology
    @@toppar/protein-allhdg5-4.top
    @@toppar/protein_break.top
    @@toppar/ion.top
    @@toppar/ligand.top
end

!
! Read coordinates and calculate energies
!

segment
  chain
    separate=true
    @@toppar/protein-allhdg5-4.link
    coordinates @@$coorfile
  end
end

coordinates @@$coorfile
delete sele=(hydrogen and attr charge = 0) end

!
! Set energy flags
!
flags
    exclude *
    include vdw elec
end

!
! Get interfacial residues of chain A (receptor)
! Assume chains A & B
!
do (store1 = 1) (segid A and
                        (not segid A) around $interface_dist_cutoff)

evaluate ($buffer_file = $coorfile - ".pdb" + ".pwa_ene")
buffer energies to=file=$buffer_file end
buffer energies
    display # Pairwise Interface Atom-Atom Energies ($coorfile)
end

for $id_a in id (attr store1 = 1) loop res_a
    show (resid) (id $id_a)
    evaluate ($resi_a = $result)
    show (resn) (id $id_a)
    evaluate ($resn_a = $result)
    show (name) (id $id_a)
    evaluate ($name_a = $result)

    for $id_b in id ( segid B and (segid A and resid $resi_a and name $name_a) around $interface_dist_cutoff ) loop res_b
        show (resid) (id $id_b)
        evaluate ($resi_b = $result)
        show (resn) (id $id_b)
        evaluate ($resn_b = $result)
        show (name) (id $id_b)
        evaluate ($name_b = $result)

        igroup interaction (segid A and resid $resi_a and name $name_a) (segid B and resid $resi_b and name $name_b) end
        energy end
        buffer energies
            display # A $resi_a $resn_a $name_a <> B $resi_b $resn_b $name_b - vdW = $vdw elec = $elec
        end
    end loop res_b

end loop res_a

buffer energies dump end

stop
